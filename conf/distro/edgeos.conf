
# (Optionally) pull in a private secrets file if present
# include conf/distro/include/mender-secrets.inc

PREFERRED_PROVIDER_mender = "mender"
PREFERRED_VERSION_mender = "5.0.%"
# PREFERRED_VERSION_mender = "5.%"
MENDER_SERVER_URL = "https://docker.mender.io"

require conf/distro/poky.conf
require conf/distro/include/mender.inc
require conf/distro/include/flash-image-sizes.inc
require systemd.conf
require conf/distro/include/l4t-r36-4-4.conf

# Network management with NetworkManager
DISTRO_FEATURES:append = " networkmanager"
VIRTUAL-RUNTIME_net_manager = "networkmanager"

# Optional: Use systemd-resolved for DNS (alternative to dnsmasq)
# DISTRO_FEATURES:append = " resolved"

# Enable automatic data partition growth on first boot
# This allows the data partition to expand to use all available disk space
MENDER_FEATURES_ENABLE:append = " mender-growfs-data"

DISTRO = "edgeos"
DISTROOVERRIDES = "poky"

DISTRO_NAME = "edgeOS Linux platform"
DISTRO_VERSION = "0.10.3"
DISTRO_CODENAME = "scarthgap"

SDK_VENDOR = "-edgeossdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION')}"
SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_VERSION}"

# MAINTAINER = "name <email>"

# remove distro unnecessary feature(s)
DISTRO_FEATURES:remove = " \
    x11 \
    wayland \
    sysvinit \
    ptest \
    3g \
    "

# Add OpenGL support for TensorRT
DISTRO_FEATURES:append = " opengl"

# Add virtualization support for NVIDIA Container Toolkit
DISTRO_FEATURES:append = " virtualization"

# Add polkit for rtkit support (real-time audio priority)
DISTRO_FEATURES:append = " polkit"

# Set the '/var/log' location, i.e. either on persistent storage
# of on volatile filesystem
#
# [Note]
# On newer Yocto versions, the 'VOLATILE_LOG_DIR' becomes deprecated
# and the replacement is via the 'FILESYSTEM_PERMS_TABLES' variable.
VOLATILE_LOG_DIR = "${@'no' if d.getVar('EDGEOS_PERSIST_JOURNAL_LOGS') == '1' else 'yes'}"

PACKAGE_CLASSES ?= "package_rpm"
# PACKAGE_CLASSES = "package_ipk"

SANITY_TESTED_DISTROS ?= " \
    ubuntu-20.04 \n\
    ubuntu-22.04 \n\
    ubuntu-24.04 \n\
    "

# require conf/distro/include/poky-world-exclude.inc
# require conf/distro/include/no-static-libs.inc
# require conf/distro/include/yocto-uninative.inc
# require conf/distro/include/security_flags.inc

USE_REDUNDANT_FLASH_LAYOUT_DEFAULT ?= "1"

# INHERIT += "tegra-support-sanity"
# ESDK_CLASS_INHERIT_DISABLE:append = " tegra-support-sanity"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"

# Enable creation of SPDX manifests by default
INHERIT += "create-spdx"

INHERIT += " \
    uninative \
    mender-setup \
    "
